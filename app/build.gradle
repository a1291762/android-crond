apply plugin: 'com.android.application'

Properties versionProps = new Properties()
def versionPropsFile = file('../version.properties')
if(versionPropsFile.exists())
    versionProps.load(new FileInputStream(versionPropsFile))
def code = (versionProps['VERSION_CODE'] ?: "1").toInteger()

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "net.yasmar.crond"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode code
        versionName "v1.3 ("+code+")"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    namespace 'net.yasmar.crond'

    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.5.1'
    api 'com.cronutils:cron-utils:4.1.3'
    implementation 'eu.chainfire:libsuperuser:1.1.1'
    // Google messed up something (duplicate class error), apparently this fixes it
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
}
